group = "$dfxengineGroup"

subprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "signing"

    group = "$dfxengineGroup"

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
        withJavadocJar()
        withSourcesJar()
    }

    repositories {
        mavenCentral()
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = new File(rootDir.path + "/secrets/username").text
                password = new File(rootDir.path + "/secrets/password").text
            }
        }
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                versionMapping {
                    usage("java-api") {
                        fromResolutionOf("runtimeClasspath")
                    }
                    usage("java-runtime") {
                        fromResolutionResult()
                    }
                }
                pom {
                    url = "https://engine.datafox.me"
                    scm {
                        connection = "scm:git:git://github.com/melodicore/dfxengine.git"
                        developerConnection = "scm:git:ssh://github.com/melodicore/dfxengine.git"
                        url = "https://github.com/melodicore/dfxengine"
                    }
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "datafox"
                            name = "Lauri \"datafox\" Heino"
                            email = "datafox@datafox.me"
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = new File(rootDir.path + "/secrets/username").text
                    password = new File(rootDir.path + "/secrets/password").text
                }
            }
        }
    }

    signing {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }
}